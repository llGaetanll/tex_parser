linespace = _{ " " | "\t" }
WHITESPACE = _{ linespace | NEWLINE }
COMMENT = _{ !("\\") ~ "%" ~ (!NEWLINE ~ ANY)* }

file = _{ SOI ~ expr* }
expr = _{
    cmd
  | scope
  | math
  | text
  | special
}


//
// COMMANDS
//
cmd = ${
  cmd_call ~ (WHITESPACE* ~ (cmd_opts | scope))*
}

cmd_call = ${ "\\" ~ cmd_name }
cmd_name = @{ ASCII_ALPHA+ ~ ("*")? }

cmd_opts = !{
    "[" ~ "]"
  | "[" ~ (cmd_opt ~ ","?)* ~ "]"
}

cmd_opt = _{ cmd_opt_kv | cmd_opt_v }

cmd_opt_k = { ASCII_ALPHA+ }
cmd_opt_v = { cmd | (ASCII_ALPHANUMERIC | ".")+ } // TODO: the text isn't quite right
cmd_opt_kv = { cmd_opt_k ~ "=" ~ cmd_opt_v }


//
// SCOPE
//
scope = { scope_open ~ scope_content ~ scope_close }
scope_content = { expr* }
scope_open = @{ !("\\") ~ "{" }
scope_close = @{ !("\\") ~ "}" }


//
// TEXT
//
text = { (escaped | char)+ }
char = _{
   !(cmd_call | scope_open | scope_close | special) ~ ASCII
}

special = _{
    math_inline_delim
  | math_multiline_open
  | math_multiline_close
  | linebreak
}

escaped = _{ "\\{" | "\\}" | "\\\"" }
linebreak = { "\\\\" }


//
// MATH
//
math_inline_delim = _{ "$" }
math_multiline_open = _{ "\\[" }
math_multiline_close = _{ "\\]" }

math = {
  (math_inline_delim ~ math_inline ~ math_inline_delim)
| (math_multiline_open ~ math_multiline ~ math_multiline_close)
}

math_inline = { (cmd | text)+ }
math_multiline = { (cmd | text)+ }
